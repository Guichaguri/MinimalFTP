plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'org.jreleaser' version '1.18.0'
}

group = 'com.guichaguri'
version = '1.0.7'

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

java {
    withSourcesJar()
    withJavadocJar()
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        minimalFtpPub(MavenPublication) {
            from components.java
            pom {
                artifactId = 'minimalftp'
                name = 'MinimalFTP'
                description = 'A lightweight, simple FTP server. Pure Java, no libraries.'
                url = 'https://github.com/Guichaguri/MinimalFTP'
                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Guichaguri'
                        name = 'Guilherme Chaguri'
                        email = 'guichaguri@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/Guichaguri/MinimalFTP.git'
                    developerConnection = 'scm:git:ssh://github.com:Guichaguri/MinimalFTP.git'
                    url = 'https://github.com/Guichaguri/MinimalFTP'
                }
            }
        }
    }
    repositories {
        maven {
            name = "LocalMavenWithChecksums"
            url = uri(layout.buildDirectory.dir("staging-deploy"))
        }

        maven {
            name = "PreDeploy"
            url = uri(layout.buildDirectory.dir("pre-deploy"))
        }
    }
}

signing {
    setRequired {
        gradle.taskGraph.allTasks.any { it.name.contains("LocalMavenWithChecksums") }
    }
    sign publishing.publications.minimalFtpPub
}

jreleaser {
    project {
        copyright = 'guichaguri.com'
        description = 'A lightweight, simple FTP server. Pure Java, no libraries.'
    }
    signing {
        active = 'ALWAYS'
        armored = true
        mode = 'FILE'
        publicKey = 'public.key'
        secretKey = 'private.key'
    }
    deploy {
        maven {
            // New accounts way
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    applyMavenCentralRules = true
                    stagingRepository('build/pre-deploy')
                }
            }
        }
    }
    release {
        github {
            enabled = false
        }
    }
}
