group 'com.guichaguri'
version '1.0.6'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

bintray {
    def publishSettings = new Properties()
    def propFile = file('publish.properties')
    if (propFile.exists()) {
        publishSettings.load(new FileInputStream(propFile))
    }

    user = publishSettings.bintray_user
    key = publishSettings.bintray_key
    publications = ['MavenBuild']
    pkg {
        repo = 'maven'
        name = 'MinimalFTP'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Guichaguri/MinimalFTP.git'

        version {
            name = project.version

            gpg {
                sign = Boolean.parseBoolean(publishSettings.gpg_sign)
                passphrase = publishSettings.gpg_passphrase
            }

            mavenCentralSync {
                sync = Boolean.parseBoolean(publishSettings.maven_sync)
                user = publishSettings.maven_user
                password = publishSettings.maven_password
            }
        }
    }
}

def pomConfig = {
    licenses {
        license {
            name "Apache License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "Guichaguri"
            name "Guilherme Chaguri"
            email "guichaguri@gmail.com"
        }
    }

    scm {
        url "https://github.com/Guichaguri/MinimalFTP/tree/master"
    }
}

// Create the publication with the pom configuration:
publishing {
    publications {
        MavenBuild(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'minimalftp'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A lightweight, simple FTP server. Pure Java, no libraries.')
                root.appendNode('name', project.name)
                root.appendNode('url', 'https://github.com/Guichaguri/MinimalFTP')
                root.children().last() + pomConfig
            }
        }
    }
}
